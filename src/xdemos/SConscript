Import('*')

if env['platform'] != 'linux':
    Return()

pbutil = env.StaticLibrary(
    target = 'pbutil',
    source = ['pbutil.c']
)

progs_env = progs_env.Clone()

progs_env.Append(LIBS = [pbutil])

progs = [
    'glsync',
    'glthreads',
    'glxcontexts',
    'glxdemo',
    'glxgears',
    'glxgears_fbconfig',
    'glxgears_pixmap',
    'glxheads',
    'glxinfo',
    'glxpbdemo',
    'glxpixmap',
    'glxsnoop',
    'glxswapcontrol',
    'manywin',
    'multictx',
    'offset',
    'opencloseopen',
    'overlay',
    'pbdemo',
    'pbinfo',
    'shape',
    'sharedtex',
    'sharedtex_mt',
    'texture_from_pixmap',
    'wincopy',
    'xfont',
    'yuvrect_client',
]

for prog in progs:
    progs_env.Program(
        target = prog,
        source = prog + '.c',
    )
    
progs_env.Program(
    target = 'corender',
    source = [
        'corender.c',
        'ipc.c',
    ]
)

progs_env.Program(
    target = 'xrotfontdemo',
    source = [
        'xrotfontdemo.c',
        'xuserotfont.c',
    ]
)


datafiles = env.Glob('*.dat')

for datafile in datafiles:
    env.Command(
        target = datafile,
        source = datafile.srcnode(),
        action = [Copy('$TARGET', '$SOURCE')],
    )
