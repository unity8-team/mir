Import('*')

if env['platform'] == 'embedded':
    Return()

SConscript([
    'util/SConscript',
])

Import('util')

progs_env = env.Clone()

if progs_env['platform'] == 'windows':
    progs_env.Append(CPPDEFINES = ['NOMINMAX'])
    progs_env.Prepend(LIBS = [
        'winmm',
        'kernel32',
        'user32',
        'gdi32',
    ])

# OpenGL, GLUT, GLEW
if progs_env['platform'] == 'windows':
    progs_env.Prepend(LIBS = ['glut32', 'glu32', 'opengl32'])
else:
    progs_env.Tool('x11')
    progs_env.Prepend(LIBS = ['GLEW', 'glut', 'GLU', 'GL'])

progs_env.Prepend(CPPPATH = [
    '#src/util',
])

progs_env.Prepend(LIBS = [
    util,
])

Export('progs_env')

SConscript([
    'images/SConscript',
    'demos/SConscript',
    'glsl/SConscript',
    'redbook/SConscript',
    'samples/SConscript',
    'tests/SConscript',
    'trivial/SConscript',
    'vp/SConscript',
    'vpglsl/SConscript',
    'fp/SConscript',
    'wgl/SConscript',
    'perf/SConscript',
])
