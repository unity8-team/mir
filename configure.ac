#  Copyright 2005 Adam Jackson.
#
#  Permission is hereby granted, free of charge, to any person obtaining a
#  copy of this software and associated documentation files (the "Software"),
#  to deal in the Software without restriction, including without limitation
#  on the rights to use, copy, modify, merge, publish, distribute, sub
#  license, and/or sell copies of the Software, and to permit persons to whom
#  the Software is furnished to do so, subject to the following conditions:
#
#  The above copyright notice and this permission notice (including the next
#  paragraph) shall be included in all copies or substantial portions of the
#  Software.
#
#  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
#  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
#  FITNESS FOR A PARTICULAR PURPOSE AND NON-INFRINGEMENT.  IN NO EVENT SHALL
#  ADAM JACKSON BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER
#  IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
#  CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
#
# Process this file with autoconf to produce a configure script

AC_PREREQ(2.57)
AC_INIT([xf86-video-nouveau],
        0.0.10,
        [https://bugs.freedesktop.org/enter_bug.cgi?product=xorg],
        xf86-video-nouveau)

AC_DEFINE_UNQUOTED([NV_MAJOR_VERSION],
                   [$(echo $PACKAGE_VERSION | sed -e 's/^\([[0-9]]\)\.[[0-9]]\.[[0-9]]/\1/')],
                   [Major version])
AC_DEFINE_UNQUOTED([NV_MINOR_VERSION],
                   [$(echo $PACKAGE_VERSION | sed -e 's/^[[0-9]]\.\([[0-9]]\)\.[[0-9]]/\1/')],
                   [Minor version])
AC_DEFINE_UNQUOTED([NV_PATCHLEVEL],
                   [$(echo $PACKAGE_VERSION | sed -e 's/^[[0-9]]\.[[0-9]]\.\([[0-9]]\)/\1/')],
                   [Patch version])
AC_DEFINE_UNQUOTED([NV_DRIVER_DATE],
                   [$(echo -n \";git log |head -3|tail -1|tr -d '\n';echo -n \")],
                   [Driver date])

AC_CONFIG_SRCDIR([Makefile.am])
AM_CONFIG_HEADER([config.h])
AC_CONFIG_AUX_DIR(.)

AM_INIT_AUTOMAKE([dist-bzip2])

# Checks for programs.
AC_DISABLE_STATIC
AC_PROG_LIBTOOL
AC_PROG_CC

AH_TOP([#include "xorg-server.h"])

AC_ARG_WITH(xorg-module-dir,
            AC_HELP_STRING([--with-xorg-module-dir=DIR],
                           [Default xorg module directory [[default=$libdir/xorg/modules]]]),
            [moduledir="$withval"],
            [moduledir="$libdir/xorg/modules"])

# Checks for extensions
XORG_DRIVER_CHECK_EXT(RANDR, randrproto)
XORG_DRIVER_CHECK_EXT(RENDER, renderproto)
XORG_DRIVER_CHECK_EXT(XV, videoproto)
XORG_DRIVER_CHECK_EXT(DPMSExtension, xextproto)

# Checks for pkg-config packages
PKG_CHECK_MODULES(LIBDRM_NOUVEAU, libdrm_nouveau)
AC_SUBST(LIBDRM_NOUVEAU_CFLAGS)
AC_SUBST(LIBDRM_NOUVEAU_LIBS)

PKG_CHECK_MODULES(XORG, [xorg-server >= 1.4.99.901] xproto fontsproto libdrm xf86driproto $REQUIRED_MODULES)
sdkdir=$(pkg-config --variable=sdkdir xorg-server)

# Checks for libraries.
AC_CHECK_DECL(XSERVER_LIBPCIACCESS,
		[XSERVER_LIBPCIACCESS=yes],[XSERVER_LIBPCIACCESS=no],
		[#include "xorg/xorg-server.h"])

if test x$XSERVER_LIBPCIACCESS = xyes; then
	PKG_CHECK_MODULES([PCIACCESS], [pciaccess >= 0.10])
fi

AM_CONDITIONAL(XSERVER_LIBPCIACCESS, test "x$XSERVER_LIBPCIACCESS" = xyes)

# Checks for header files.
AC_HEADER_STDC

# Use -Wall all the time
CFLAGS="$CFLAGS -Wall"

# Check for -minline-all-stringops availabtility
OLD_CFLAGS="$CFLAGS"
CFLAGS="$CFLAGS -minline-all-stringops"
AC_COMPILE_IFELSE([AC_LANG_PROGRAM([[ #include <stdio.h> ]], [[ ]])],
[ CFLAGS="$OLD_CFLAGS -minline-all-stringops"],[CFLAGS="$OLD_CFLAGS"])

# needed for the next test
CFLAGS="$CFLAGS $XORG_CFLAGS"

AC_MSG_CHECKING(for DRM modesetting)
AC_ARG_WITH(
	kms,
	[  --with-kms=yes,no       compile with drm modesetting support (default: auto)],
	kms="$withval",
	kms="yes")
AC_MSG_RESULT($kms)

if test "x$kms" = xyes; then
	AC_CHECK_HEADER([xf86drmMode.h],[kms=yes],[kms=no],
		[#include "stdint.h"    ]
	)
fi

if test "x$kms" = xyes; then
	AC_DEFINE(XF86DRM_MODE,1,[DRM kernel modesetting])
fi

AC_SUBST([CFLAGS])
AC_SUBST([moduledir])

DRIVER_NAME=nouveau
AC_SUBST([DRIVER_NAME])

XORG_MANPAGE_SECTIONS
XORG_RELEASE_VERSION

AC_OUTPUT([
	Makefile
	src/Makefile
	man/Makefile
])
